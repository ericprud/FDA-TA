# get a narrative of what happened to each patient as a result of each regimen of medication.

# ┌────────────────┬──────────────────────────┬──────────────────────┬─────┬──────────────────────────┬────────────────────┬─────┐
# │ ?who           │ ?medName                 │ ?dose                │ ?x  │ ?outcomeCode             │ ?start             │ ?PN │
# │   "1#0106-026" │  "#Tacrolimus-STUDYDRUG" │  "008mg 1 per 1.0 d" │  49 │    "NonFunctioningGraft" │ "2010-11-29 19:00" │ "-" │
# │ "2#033134-018" │ "#Tacrolimus-COMPARATOR" │  "008mg 2 per 1.0 d" │   1 │    "NonFunctioningGraft" │ "2010-11-29 19:00" │ "-" │
# │ "2#049139-002" │ "#Tacrolimus-COMPARATOR" │  "003mg 2 per 1.0 d" │ 484 │   "SemiFunctioningGraft" │ "2010-11-29 19:00" │ " " │
# │ "2#049139-002" │ "#Tacrolimus-COMPARATOR" │  "003mg 2 per 1.0 d" │ 484 │    "NonFunctioningGraft" │ "2010-11-29 19:00" │ "-" │
# │          "Amy" │    "#ImmunosuppressantB" │ "50.0mg 6 per 1.0 d" │  20 │ "NormalFunctioningGraft" │ "2013-10-19 12:34" │ "+" │
# │          "Bob" │    "#ImmunosuppressantA" │ "50.0mg 4 per 1.0 d" │   9 │   "SemiFunctioningGraft" │ "2013-08-27 19:30" │ " " │
# │          "Bob" │    "#ImmunosuppressantA" │ "25.0mg 1 per 1.0 d" │   5 │   "SemiFunctioningGraft" │ "2013-08-28 14:32" │ " " │
# │          "Sue" │    "#ImmunosuppressantB" │ "50.0mg 6 per 1.0 d" │   8 │   "SemiFunctioningGraft" │ "2013-07-07 19:00" │ " " │
# │          "Sue" │    "#ImmunosuppressantB" │ "75.0mg 8 per 1.0 d" │   7 │    "NonFunctioningGraft" │ "2013-07-08 16:15" │ "-" │
# └────────────────┴──────────────────────────┴──────────────────────┴─────┴──────────────────────────┴────────────────────┴─────┘

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX dt: <http://www.hl7.org/owl/iso-dt-2.0#>
PREFIX bridg: <http://www.bridgmodel.org/owl#>
PREFIX : <http://www.w3.org/2013/12/FDA-TA/tests/RenalTransplantation/subject-bob#>
PREFIX ucum: <http://unitsofmeasure.org/ucum-essence.xml#>
PREFIX core: <http://www.w3.org/2013/12/FDA-TA/core#>
PREFIX data: <http://www.w3.org/2013/12/FDA-TA/datatypes#>
PREFIX RenalX: <http://www.w3.org/2013/12/FDA-TA/RenalTransplantation#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

  SELECT ?who ?medName ?dose ?x
         (STRAFTER(STR(?outcomeVal), '#') AS ?outcomeCode)
         (CONCAT(SUBSTR(STR(?time), 0, 10), ' ' ,SUBSTR(STR(?time), 12, 5)) AS ?start)
         (IF(BOUND(?outcomeSense) , IF(?outcomeSense = core:PositiveOutcome , '+', '-'), ' ') AS ?PN)
   WHERE {
        ?subject bridg:Subject.performingBiologicEntity [ foaf:name ?who ];
                 core:hasOutcomeAssessment ?outcome .
        ?outcome core:hasObservationTime ?time ;
                 core:hasResultValue ?outcomeVal
        { ?outcome a RenalX:KidneyGraftOutcomeAssessment } UNION { ?outcome a core:DeathOtherReasons }
        OPTIONAL {
          ?outcome a ?outcomeSense ;
          FILTER ( ?outcomeSense = core:PositiveOutcome || ?outcomeSense = core:NegativeOutcome )
        }
	OPTIONAL {
	  SELECT ?outcome (substr(str(?med), 39) AS ?medName) (CONCAT(STR(?qty), 'mg ', STR(?frq), ' per ', STR(?dur), ' ' , ?schedunit) AS ?dose) (COUNT(*) AS ?x)
	   WHERE {
	     ?outcome core:hasIntervention [ bridg:PerformedActivity.instantiatedScheduledActivity [
                     bridg:ScheduledActivity.dateRange [ dt:IVL_TS.DATETIME.low ?time ] ;
                     core:medication ?med ;
                     core:hasDosagePlan [
                         core:hasMedicationQuantity [
                             data:value ?qty ; data:units ucum:mg ] ;
                         core:hasMedicationSchedule [
                             core:hasMedicationFrequency ?frq ;
                             core:hasMedicationDuration ?dur ;
                             core:hasMedicationUnits ?schedunit ]
                     ]
                 ]
             ]
           } GROUP BY ?outcome
	}
    } ORDER BY ?who ?time

