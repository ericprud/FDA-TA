# view the rates of outcomes across n studies in one TA.

# ┌───────────────────────────────────┬────────┬────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐
# │ ?endpoint                         │ ?count │ ?labels                                                                                                                        │
# │                   "GraftSurvival" │      1 │                                                                                                                          "Amy" │
# │                       "GraftLoss" │      4 │                                                                                     "Sue 1#0106-026 2#033134-018 2#049139-002" │
# │ "AcuteRejection indicated by GFR" │      4 │ "Sue(2013-07-08T14:50:00) 1#0106-026(2013-07-08T14:50:00) 2#033134-018(2013-07-08T14:50:00) 2#049139-002(2013-07-08T14:50:00)" │
# │ "AcuteRejection indicated by SCr" │      1 │                                                                                                    "Sue(2013-07-08T14:22:00Z)" │
# │                           "Death" │      1 │                                                                                                                          "Bob" │
# │                            "LTFU" │      0 │                                                                                                                             "" │
# └───────────────────────────────────┴────────┴────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bridg: <http://www.bridgmodel.org/owl#>
PREFIX core: <http://www.w3.org/2013/12/FDA-TA/core#>
PREFIX xplant: <http://www.w3.org/2013/12/FDA-TA/transplant#>
PREFIX renal: <http://www.w3.org/2013/12/FDA-TA/renal#>
PREFIX data: <http://www.w3.org/2013/12/FDA-TA/datatypes#>

SELECT ?endpoint ?count ?labels {
  {
    SELECT ("GraftSurvival" AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?name) AS ?labels)
    WHERE {
      BIND (1 AS ?g)
      [
        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
        core:hasOutcomeAssessment [
          a xplant:GraftSurvivalAssessment ]
      ]
    } GROUP BY ?g
  }
  UNION
  {
    SELECT ("GraftLoss" AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?name) AS ?labels)
    WHERE {
      BIND (1 AS ?g)
      [
        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
        core:hasOutcomeAssessment [
          a xplant:GraftLossAssessment ]
      ]
    } GROUP BY ?g
  }
  UNION
#  {
#    SELECT ("DecreasingGFR" AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?label) AS ?labels)
#    WHERE {
#      BIND (1 AS ?g)
#      [ 
#        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
#        core:hasOutcomeAssessment ?oa ] .
#          ?oa a renal:RenalFunctionOutcomeAssessment ; core:hasObservationTime ?when .
#	  BIND (CONCAT(?name, '(', STR(?when), ')') AS ?label)
#          { SELECT ?oa { ?oa core:hasObservation [ a renal:GfrFlowRateObservation ] } GROUP BY ?oa } # over GFRs
#          MINUS { # occurances of GFR increasing with date
#            ?oa core:hasObservation
#                    [ a renal:GfrFlowRateObservation ; core:hasObservationTime ?date1 ; core:hasResultValue [ data:value ?gfr1 ] ] ,
#                    [ a renal:GfrFlowRateObservation ; core:hasObservationTime ?date2 ; core:hasResultValue [ data:value ?gfr2 ] ]
#            FILTER (?date2 > ?date1 && ?gfr2 > ?gfr1)
#          }
#    } GROUP BY ?g
#  }
#  UNION
#  {
#    SELECT ("IncreasingSCr" AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?label) AS ?labels)
#    WHERE {
#      BIND (1 AS ?g)
#      [ 
#        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
#        core:hasOutcomeAssessment ?oa ] .
#          ?oa a renal:RenalFunctionOutcomeAssessment ; core:hasObservationTime ?when .
#	  BIND (CONCAT(?name, '(', STR(?when), ')') AS ?label)
#          { SELECT ?oa { ?oa core:hasObservation [ a renal:SerumCreatinineObservation ] } GROUP BY ?oa } # over SCrs
#          MINUS { # occurances of SCr decreasing with date
#            ?oa core:hasObservation
#                    [ a renal:SerumCreatinineObservation ; core:hasObservationTime ?date1 ; core:hasResultValue [ data:value ?scr1 ] ] ,
#                    [ a renal:SerumCreatinineObservation ; core:hasObservationTime ?date2 ; core:hasResultValue [ data:value ?scr2 ] ]
#            FILTER (?date2 > ?date1 && ?scr2 < ?scr1)
#          }
#    } GROUP BY ?g
#  }
#  UNION
  {
    SELECT (CONCAT("AcuteRejection indicated by ", ?l) AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?label) AS ?labels)
    WHERE {
      []
        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
        core:hasOutcomeAssessment ?oa .
        ?oa core:afterIntervention ?ai .
        ?ai core:hasObservationTime ?t .
        ?ai bridg:PerformedActivity.instantiatedDefinedActivity ?da .
        ?da rdfs:label ?l .
#        MINUS { ?oa core:afterIntervention ?ai, ?ai2 .
#            ?ai core:hasObservationTime ?t .
#            ?ai2 core:hasObservationTime ?t2 .
#            FILTER (?t2 > ?t)
#        }
        BIND (CONCAT(?name, "(", STR(?t), ")") AS ?label)
        ?oa core:hasResultValue renal:CeasedToFunction .
    } GROUP BY ?da
  }
  UNION
  {
    SELECT ("Death" AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?name) AS ?labels)
    WHERE {
      BIND (1 AS ?g)
      [
        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
        core:hasDisposition [ core:hasResultValue core:Deceased ]
      ]
    } GROUP BY ?g
  }
  UNION
  {
    SELECT ("LTFU" AS ?endpoint) (COUNT(*) AS ?count) (GROUP_CONCAT(?name) AS ?labels)
    WHERE {
      BIND (1 AS ?g)
      [
        bridg:Subject.performingBiologicEntity [ foaf:name ?name ] ;
        core:hasDisposition [ core:hasResultValue core:LostToFollowUp ]
      ]
    } GROUP BY ?g
  }
}

# ┌───────────────────────────────────┬────────┬─────────────────────────────┐
# │ ?endpoint                         │ ?count │ ?labels                     │
# │                   "GraftSurvival" │      1 │                       "Amy" │
# │                       "GraftLoss" │      1 │                       "Sue" │
# │ "AcuteRejection indicated by GFR" │      1 │  "Sue(2013-07-08T14:50:00)" │
# │ "AcuteRejection indicated by SCr" │      1 │ "Sue(2013-07-08T14:22:00Z)" │
# │                           "Death" │      1 │                       "Bob" │
# │                            "LTFU" │      0 │                          "" │
# └───────────────────────────────────┴────────┴─────────────────────────────┘
