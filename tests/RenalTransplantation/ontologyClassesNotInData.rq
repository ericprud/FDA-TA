# sparql -d work.trig ontologyClassesNotInData.rq -L text/raw

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT ?class
       ("\n" AS ?EOL)
 WHERE
{
  {
          { ?x rdfs:domain ?class }
    UNION { { ?x rdfs:range  ?class }
            MINUS { ?x a    owl:DatatypeProperty } }
    UNION { ?x rdfs:subClassOf ?class }
    UNION { ?class rdfs:subClassOf ?x }
    UNION { ?class a    owl:class }
  }
  FILTER (!isBlank(?class) && !strstarts(str(?class), str(owl:)))
  MINUS
  {
    {
      ?w a           ?class
    } UNION {
      ?class owl:oneOf ?l .
      ?l rdf:rest*/rdf:first ?o .
      ?s ?p ?o .
    }
  }
} ORDER BY ?class

